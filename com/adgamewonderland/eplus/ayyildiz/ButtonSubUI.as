/* 
 * Generated by ASDT 
*/ 

import com.adgamewonderland.eplus.ayyildiz.*;

class com.adgamewonderland.eplus.ayyildiz.ButtonSubUI extends MovieClip {
	
	private var myId:Number; // id steht auf zeitleiste
	
	private var myNavigation:NavigationUI;
	
	private var myNaviMain:NaviMainUI;
	
	private var isUnderlined:Boolean;
	
	private var hitarea_mc:MovieClip;
	
	public function ButtonSubUI() 
	{
		// navigation
		myNavigation = NavigationUI(_parent._parent);
		// hauptnavigationspunkt
		myNaviMain = NaviMainUI(_parent);
		// nicht unterstrichen
		underlined = false;
		// hitarea
		hitArea = hitarea_mc;
	}
	
	public function set id(num:Number ):Void
	{
		// id steht auf zeitleiste
		myId = num;
		// registrieren und pruefen, ob zu aktueller seite passend
		var selected:Boolean = myNavigation.registerButtonSub(myNaviMain.id + NavigationUI.DELIMITER + id);
		// unterstreichen
		underlined = selected;
	}
	
	public function get id():Number
	{
		// id steht auf zeitleiste
		return myId;
	}
	
	public function get underlined():Boolean
	{
		// unterstrichen
		return isUnderlined;
	}
	
	public function set underlined(bool:Boolean ):Void
	{
		// schalter umlegen
		isUnderlined = bool;
		// unterstrich ein- / ausblenden
		gotoAndStop(bool ? 3 : 2);
	}
	
	public function onRollOver():Void
	{
		// unterstrich einblenden
		underlined = true;
	}
	
	public function onRollOut():Void
	{
		// unterstrich ausblenden
		underlined = false;
	}
	
	public function onReleaseOutside():Void
	{
		onRollOut();
	}
	
	public function onRelease():Void
	{
		// navi schliessen und neue seite laden lassen
		myNaviMain.closeNavi(id);
	}
}