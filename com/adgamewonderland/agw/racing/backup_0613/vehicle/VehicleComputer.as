/* 
 * Generated by ASDT 
*/ 

/*
klasse:			VehicleComputer
autor: 			gerd jungbluth, adgame-wonderland
email:			gerd.jungbluth@adgame-wonderland.de
kunde:			agw
erstellung: 		21.05.2005
zuletzt bearbeitet:	29.05.2005
durch			gj
status:			in bearbeitung
*/

import com.adgamewonderland.agw.math.*;

import com.adgamewonderland.agw.racing.race.*;

import com.adgamewonderland.agw.racing.track.*;

import com.adgamewonderland.agw.racing.vehicle.*;

class com.adgamewonderland.agw.racing.vehicle.VehicleComputer extends Vehicle {
	
	public static var STRENGTH_SLOW:Number = 1;
	
	public static var STRENGTH_MEDIUM:Number = 2;
	
	public static var STRENGTH_FAST:Number = 3;
	
	private static var VAVG:Object = {avg : 15, diff : 5};
	
	private var myStrength:Number;
	
	private var myTargetPosition:Point;
	
	private var myDirection:Vector;
	
	public function VehicleComputer()
	{
		// eigenschaften des vehicle
		super.constructor.apply(super, arguments);
		// typ des vehicles
		type = TYPE_COMPUTER;
		// staerke
		myStrength = STRENGTH_MEDIUM;
		// zielposition
		myTargetPosition = new Point(0, 0);
		// aktuelle fahrtrichtung
		myDirection = new Vector(0, 0);
	}
	
	public function set strength(num:Number ):Void
	{
		// staerke
		myStrength = num;
	}
	
	public function get targetposition():Point
	{
		// zielposition
		return myTargetPosition;
	}
	
	public function get direction():Vector
	{
		// aktuelle fahrtrichtung
		return myDirection;
	}
	
	public function onUpdateStroke():Void
	{
		// beschleunigung updaten
		updateAcceleration();
		// geschwindigkeit updaten
		updateVelocity();
		// auf durchschnittsgeschwindigkeit
		velocity.setLength(VAVG.avg - (STRENGTH_MEDIUM - myStrength) * VAVG.diff);
		// drehwinkel
		myAngle = velocity.getAngle();
		
		// position updaten
		updatePosition();
		// movieclip updaten
		updateMovieclip();
		// testen, ob fahrzeug auf strecke
		ontrack = controller.isVehicleOnTrack();
		// was passiert, wenn das fahrzeug nicht auf der strecke ist?
		if (!ontrack) whileNotOnTrack();
		// sound updaten
//		updateSound();
		// controller informieren
		RaceController.getInstance().onVehicleMove(this);
	}
	
	public function onChangeTrackpiece():Void
	{
		// zielposition auf aktuelles streckenteil
//		updateTargetPosition(VehicleController.TRACKPIECE_CURRENT);
		updateTargetPosition(VehicleController.TRACKPIECE_NEXT);
	}
	
	private function updateTargetPosition(type:Number ):Void
	{
		// streckenteil, in dessen richtung das fahrzeug fahren soll
		var trackpiece:TrackPiece = controller.getTrackpiece(type);
		// schwerpunkt
		var center:Point = trackpiece.polygon.center;
		// zielposition updaten
		targetposition.x = center.x;
		targetposition.y = center.y;
	}
	
	private function updateAcceleration():Void
	{
		// aktuelle fahrtrichtung
		direction.setDiff((targetposition.x - position.x), (targetposition.y - position.y));
		// winkel der aktuellen fahrtrichtung
		var angle:Number = direction.getAngle();
		// testen, ob fast erreicht
		if (direction.getLength() < 50) {
			// zielposition auf naechstes streckenteil
			updateTargetPosition(VehicleController.TRACKPIECE_NEXT);
		}
		// beschleunigen
		acceleration.setLength(5);
		// beschleunigung drehen
		acceleration.setAngle(angle);
		
		// winkeldifferenz zwischen beschleunigung und geschwindigkeit
//		var adiff:Number = acceleration.getAngle() - velocity.getAngle();
		// geschwindigkeit drehen
//		velocity.setAngle(velocity.getAngle() + adiff / 2);
	}
}