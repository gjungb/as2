/* 
 * Generated by ASDT 
*/ 

import com.adgamewonderland.sskddorf.remote.*;

class com.adgamewonderland.sskddorf.remote.ControllerUI extends MovieClip {
	
	private var mySound:Sound;
	
	private var myDescriptions:Array;
	
	private var snd_btn:Button;
	
	private var window_btn:Button;
	
	private var power_btn:Button;
	
	public function ControllerUI()
	{
		// globaler sound
		mySound = new Sound();
		// descriptions zu den buttons
		myDescriptions = new Array();
		
		// descriptions einblenden
		snd_btn.onRollOver = function () {
			this._parent.showDescription(1);
		};
		window_btn.onRollOver = function () {
			this._parent.showDescription(2);
		};
		power_btn.onRollOver = function () {
			this._parent.showDescription(3);
		};
		
		// descriptions ausblenden
		snd_btn.onRollOut = window_btn.onRollOut = power_btn.onRollOut = function () {
			this._parent.hideDescription();
		};
		
		// sound ein- / ausschalten
		snd_btn.onRelease = function () {
			this._parent.toggleSound();
		};
		// fenster im vordergund halten
		window_btn.onRelease = function () {
			this._parent.toggleWindow();
		};
		// fernbedienung schliessen
		power_btn.onRelease = function () {
			this._parent.closeWindow();
		};
	}
	
	public function set descriptions(descriptions:Array ):Void
	{
		myDescriptions = descriptions;
	}
	
	public function toggleSound():Void
	{
		// lautstaerke umschalten
		mySound.setVolume(mySound.getVolume() > 0 ? 0 : 100);
	}
	
	public function toggleWindow():Void
	{
		// fixierung umschalten
		getURL("javascript:toggleWindow()", "");
	}
	
	public function closeWindow():Void
	{
		// schliessen
		getURL("javascript:closeWindow()", "");
	}
	
	public function showDescription(id:Number ):Void
	{
		// entsprechende description
		var description:String = myDescriptions[id];
		// display
		var display:DisplayUI = RemoteUI(_parent).getDisplay();
		// description anzeigen
		display.showDescription(description);
		// licht einschalten
		display.showLight(true);
	}
	
	public function hideDescription():Void
	{
		// display
		var display:DisplayUI = RemoteUI(_parent).getDisplay();
		// licht ausschalten
		display.showLight(false);
	}
}