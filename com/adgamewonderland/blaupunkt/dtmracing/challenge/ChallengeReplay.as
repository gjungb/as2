/* 
 * Generated by ASDT 
*/ 

/*
klasse:			ChallengeReplay
autor: 			gerd jungbluth, adgame-wonderland
email:			gerd.jungbluth@adgame-wonderland.de
kunde:			agw
erstellung: 		01.07.2005
zuletzt bearbeitet:	01.07.2005
durch			gj
status:			in bearbeitung
*/

import com.adgamewonderland.agw.math.*;

class com.adgamewonderland.blaupunkt.dtmracing.challenge.ChallengeReplay {
	
	public static var TDIFF:Number = 500;
	
	private static var SEPARATOR_COORDS:String = ",";
	
	private static var SEPARATOR_POINTS:String = ";";
	
	private var replay:String;
	
	private var myCounter:Number;
	
	private var myPositions:Array;
	
	public function ChallengeReplay(replay:String )
	{
		// replay als string
		this.replay = replay;
		// zaehler fuer zeittakte
		myCounter = 0;
		// gespeicherte positionen als array
		myPositions = new Array();
	}
	
	public function setReplay(str:String ):Void
	{
		// replay als string
		replay = str;
	}
	
	public function getReplay():String
	{
		// replay als string
		return replay;
	}
	
	public function addPosition(time:Number, pos:Point ):Void
	{
		// zeit kommt in ms, umrechnen in sekunden
		var second:Number = Math.floor(time / TDIFF);
		// abbrechen, wenn noch nicht genug zeit vergangen
		if (second <= myCounter) return;
		// zehler hochzaehlen
		myCounter += 1;
		// x- und y-koordinate getrennt
		replay += (Math.round(pos.x) + SEPARATOR_COORDS + Math.round(pos.y));
		// abtrennung zur naechsten
		replay += SEPARATOR_POINTS;
	}
	
	public function getPosition(step:Number ):Point
	{
		// testen, ob schon in array umgeformt
		if (myPositions.length == 0) myPositions = toArray();
		// abbrechen, wenn ganz abgespeilt
		if (step >= myPositions.length) return null;
		// position als array
		var position:Array = myPositions[step].split(SEPARATOR_COORDS);
		// als point zurueck geben
		return new Point(Number(position[0]), Number(position[1]));
	}
	
	private function toArray():Array
	{
		// in array aufspalten
		return getReplay().split(SEPARATOR_POINTS);
	}
}