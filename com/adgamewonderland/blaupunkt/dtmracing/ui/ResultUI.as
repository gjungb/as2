/* 
 * Generated by ASDT 
*/ 

/*
klasse:			ResultUI
autor: 			gerd jungbluth, adgame-wonderland
email:			gerd.jungbluth@adgame-wonderland.de
kunde:			blaupunkt
erstellung: 		14.06.2005
zuletzt bearbeitet:	22.06.2005
durch			gj
status:			in bearbeitung
*/

import mx.rpc.ResultEvent;
import mx.rpc.FaultEvent;

import com.adgamewonderland.agw.*;

import com.adgamewonderland.agw.util.*;

import com.adgamewonderland.blaupunkt.dtmracing.challenge.*;

import com.adgamewonderland.blaupunkt.dtmracing.remoting.*;

import com.adgamewonderland.blaupunkt.dtmracing.ui.*;

class com.adgamewonderland.blaupunkt.dtmracing.ui.ResultUI extends MovieClip {
	
	private var myDtmRacingUI:DtmRacingUI;
	
	private var chooser_track_mc:ChooserTrackUI;
	
	private var chooser_car_mc:ChooserCarUI;
	
	private var back_metal_mc:MovieClip;
	
	private var result_info_mc:InfoboxResultUI;
	
	private var headline_txt:TextField;
	
	private var opponent_txt:TextField;
	
	private var back_btn:Button;
	
	public function ResultUI()
	{
		// hauptzeitleiste
		myDtmRacingUI = DtmRacingUI(_parent);
		// abspielen verfolgen
		var follower:TimelineFollower = new TimelineFollower(this, "init");
		// abspielen verfolgen
		onEnterFrame = function() {
			follower.followTimeline();
		}
	}
	
	public function init():Void
	{
		// headline zentriert
		headline_txt.autoSize = "center";
		// oppponent zentriert
		opponent_txt.autoSize = "center";
		// metal hochfahren
		back_metal_mc.showMetal(true);
		// status der herausforderung
		var status:Number = ChallengeController.getInstance().getStatus();
		// headline initialisieren
		initHeadline(status);
		// streckauswahl abschalten
		chooser_track_mc.setMode(ChooserUI.MODE_SHOW);
		// fahrzeugauswahl abschalten
		chooser_car_mc.setMode(ChooserUI.MODE_SHOW);
		// infoboxen initialisieren
		result_info_mc.initInfobox(status);
	}
	
	public function sendChallenge(message:String ):Void
	{
		// nachricht des challengers speichern
		ChallengeController.getInstance().setChallengerMessage(message);
		// herausforderung senden
		ChallengeController.getInstance().sendChallenge();
		// abspielen verfolgen
		var follower:TimelineFollower = new TimelineFollower(this, "onResultFinished", DtmRacingUI.NEXT_MAIN);
		// abspielen verfolgen
		onEnterFrame = function() {
			follower.followTimeline();
		}
		// abspielen
		gotoAndPlay("frClose");
	}
	
	public function stopResult(status:Number ):Void
	{
		// je nach status
		var next:Number;
		// training
		if (status == Challenge.STATUS_TRAINING) next = DtmRacingUI.NEXT_MAIN;
		// guest
		if (status == Challenge.STATUS_GUEST) next = DtmRacingUI.NEXT_LOGIN;
		// abspielen verfolgen
		var follower:TimelineFollower = new TimelineFollower(this, "onResultFinished", next);
		// abspielen verfolgen
		onEnterFrame = function() {
			follower.followTimeline();
		}
		// abspielen
		gotoAndPlay("frClose");
	}
	
	public function startRace():Void
	{
		// abspielen verfolgen
		var follower:TimelineFollower = new TimelineFollower(this, "onResultFinished", DtmRacingUI.NEXT_RACE);
		// abspielen verfolgen
		onEnterFrame = function() {
			follower.followTimeline();
		}
		// abspielen
		gotoAndPlay("frClose");
	}
	
	public function onResultFinished(next:Number ):Void
	{
		// zur main
		if (next == DtmRacingUI.NEXT_MAIN) {
			myDtmRacingUI.showMain();
		// zum race
		} else if (next == DtmRacingUI.NEXT_RACE) {
			myDtmRacingUI.showRace();
		// zum login
		} else if (next == DtmRacingUI.NEXT_LOGIN) {
			myDtmRacingUI.showLogin();
		}
	}
	
	private function initHeadline(status:Number ):Void
	{
		// je nach status
		switch (status) {
			// herausforderung
			case Challenge.STATUS_NEW :
				// headline
				headline_txt.text = "Herausforderung";
				// opponent
				var opponent:User = ChallengeController.getInstance().getOpponent();
				// nickname oder email anzeigen
				if (opponent.getUid() != undefined) {
					opponent_txt.text = opponent.getNickname();
				} else {
					opponent_txt.text = opponent.getEmail();
				}
			
				break;
			// trainieren
			case Challenge.STATUS_TRAINING :
				// headline
				headline_txt.text = "Trainieren";
			
				break;
			// gast
			case Challenge.STATUS_GUEST :
				// headline
				headline_txt.text = "Gast";
			
				break;
			
		}
		// in versalien
		headline_txt.text = headline_txt.text.toUpperCase();
	}
	
}