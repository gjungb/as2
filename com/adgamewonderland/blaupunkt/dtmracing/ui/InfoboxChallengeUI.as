/* 
 * Generated by ASDT 
*/ 

/*
klasse:			InfoboxChallengeUI
autor: 			gerd jungbluth, adgame-wonderland
email:			gerd.jungbluth@adgame-wonderland.de
kunde:			blaupunkt
erstellung: 		09.06.2005
zuletzt bearbeitet:	26.06.2005
durch			gj
status:			in bearbeitung
*/

import com.adgamewonderland.agw.util.*;

import com.adgamewonderland.blaupunkt.dtmracing.challenge.*;

import com.adgamewonderland.blaupunkt.dtmracing.ui.*;

class com.adgamewonderland.blaupunkt.dtmracing.ui.InfoboxChallengeUI extends InfoboxUI {
	
	private var guestinfo_mc:MovieClip;
	
	private var nicknames_mc:MovieClip;
	
	private var time_mc:MovieClip;
	
	private var statistics1_mc:UserStatisticUI;
	
	private var statistics2_mc:UserStatisticUI;
	
	private var boxes1_mc:MovieClip;
	
	private var boxes2_mc:MovieClip;
	
	private var boxes3_mc:MovieClip;
	
	private var startrace_btn:Button;
	
	public function InfoboxChallengeUI()
	{
		// vererbung
		super.constructor.apply(super, arguments);
		// alle ausblenden
		statistics1_mc._visible = statistics2_mc._visible = false;
		// button rennen starten
	 	startrace_btn.onRelease = function () {
	 		this._parent._parent.startRace();
	 	};
	}
	
	public function initInfobox(status:Number ):Void
	{
		// je nach status
		switch (status) {
			// herausforderung
			case Challenge.STATUS_NEW :
				
				guestinfo_mc._visible = false;
				
				time_mc._visible = false;
				
				boxes1_mc._visible = false;
				
				boxes3_mc._visible = false;
				
				boxes2_mc.play();
				// statistik challenger laden
				statistics1_mc.init();
				// statistik opponent laden
				statistics2_mc.init();
				// nicknames anzeigen
				nicknames_mc.nickname1_txt.text = ChallengeController.getInstance().getUser().getNickname();
				// opponent
				var opponent:User = ChallengeController.getInstance().getOpponent();
				// nickname oder email anzeigen
				if (opponent.getUid() != undefined) {
					nicknames_mc.nickname2_txt.text = opponent.getNickname();
				} else {
					nicknames_mc.nickname2_txt.text = opponent.getEmail();
				}
			
				break;
			// herausforderung annehmen
			case Challenge.STATUS_CHALLENGER_DONE :
				
				guestinfo_mc._visible = false;
				
				boxes1_mc._visible = false;
				
				boxes3_mc._visible = false;
				
				boxes2_mc.play();
				// statistik challenger laden
				statistics1_mc.init();
				// statistik opponent laden
				statistics2_mc.init();
				// nicknames anzeigen
				nicknames_mc.nickname1_txt.text = ChallengeController.getInstance().getUser().getNickname();
				// opponent
				var opponent:User = ChallengeController.getInstance().getOpponent();
				// nickname oder email anzeigen
				if (opponent.getUid() != undefined) {
					nicknames_mc.nickname2_txt.text = opponent.getNickname();
				} else {
					nicknames_mc.nickname2_txt.text = opponent.getEmail();
				}
				// vorgelegte zeit anzeigen
				time_mc.racetime_txt.autoSize = "center";
				// rennzeit
				var racetime:Number = ChallengeController.getInstance().getRacetime(ChallengeDetail.MODE_CHALLENGER);
				// anzeigen
				time_mc.racetime_txt.text = TimeFormater.getMinutesSecondsMilliseconds(racetime);
			
				break;
			// trainieren
			case Challenge.STATUS_TRAINING :
				
				guestinfo_mc._visible = false;
				
				nicknames_mc._visible = false;
				
				time_mc._visible = false;
				
				statistics2_mc._visible = false;
				
				boxes2_mc._visible = false;
				
				boxes3_mc._visible = false;
				
				boxes1_mc.play();
				// statistik challenger laden
				statistics1_mc.loadUserStatistic();
			
				break;
			// gast
			case Challenge.STATUS_GUEST :
				
				nicknames_mc._visible = false;
				
				time_mc._visible = false;
				
				statistics1_mc._visible = false;
				
				statistics2_mc._visible = false;
				
				boxes1_mc._visible = false;
				
				boxes2_mc._visible = false;
				
				boxes3_mc.play();
			
				break;
			
		}
		// abspielen verfolgen
		onEnterFrame = function() {
			// nacheinander einblenden
			if (_currentframe == _totalframes - 2 && status != Challenge.STATUS_GUEST) statistics1_mc._visible = true;
			if (_currentframe == _totalframes - 1 && !(status == Challenge.STATUS_TRAINING || status == Challenge.STATUS_GUEST)) statistics2_mc._visible = true;
			if (_currentframe == _totalframes - 0) delete(onEnterFrame);
		};
		// abspielen
		play();
	}
	
	public function onChangeTrack(tid:Number, allowed:Boolean ):Void
	{
		// statistiken anzeigen lassen
		statistics1_mc.showUserTrackStatistic(tid);
		statistics2_mc.showUserTrackStatistic(tid);
		// button rennen starten umschalten
		startrace_btn.enabled = allowed;
		startrace_btn._alpha = (allowed ? 100 : 20);
	}
}