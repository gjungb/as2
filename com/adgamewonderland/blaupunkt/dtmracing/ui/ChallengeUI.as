/* 
 * Generated by ASDT 
*/ 

/*
klasse:			ChallengeUI
autor: 			gerd jungbluth, adgame-wonderland
email:			gerd.jungbluth@adgame-wonderland.de
kunde:			blaupunkt
erstellung: 		09.06.2005
zuletzt bearbeitet:	24.06.2005
durch			gj
status:			in bearbeitung
*/

import mx.rpc.ResultEvent;
import mx.rpc.FaultEvent;

import com.adgamewonderland.agw.*;

import com.adgamewonderland.agw.util.*;

import com.adgamewonderland.blaupunkt.dtmracing.challenge.*;

import com.adgamewonderland.blaupunkt.dtmracing.remoting.*;

import com.adgamewonderland.blaupunkt.dtmracing.ui.*;

class com.adgamewonderland.blaupunkt.dtmracing.ui.ChallengeUI extends MovieClip {
	
	private var myDtmRacingUI:DtmRacingUI;
	
	private var chooser_track_mc:ChooserTrackUI;
	
	private var chooser_car_mc:ChooserCarUI;
	
	private var back_metal_mc:MovieClip;
	
	private var challenge_info_mc:InfoboxChallengeUI;
	
	private var headline_txt:TextField;
	
	private var back_btn:Button;
	
	public function ChallengeUI()
	{
		// hauptzeitleiste
		myDtmRacingUI = DtmRacingUI(_parent);
		// abspielen verfolgen
		var follower:TimelineFollower = new TimelineFollower(this, "init");
		// abspielen verfolgen
		onEnterFrame = function() {
			follower.followTimeline();
		}
		// bei gast button zurueck ausblenden
		if(ChallengeController.getInstance().getStatus() == Challenge.STATUS_GUEST) back_btn._visible = false;
	}
	
	public function init():Void
	{
		// headline zentriert
		headline_txt.autoSize = "center";
	 	// button zurueck
	 	back_btn.onRelease = function () {
	 		this._parent.stopChallenge();
	 	};
		// metal hochfahren
		back_metal_mc.showMetal(true);
		// status der herausforderung
		var status:Number = ChallengeController.getInstance().getStatus();
		// headline initialisieren
		initHeadline(status);
		// fahrzeugauswahl anschalten
		chooser_car_mc.setMode(ChooserUI.MODE_SELECT);
		// infoboxen initialisieren
		challenge_info_mc.initInfobox(status);
	}
	
	public function stopChallenge():Void
	{
		// abspielen verfolgen
		var follower:TimelineFollower = new TimelineFollower(this, "onChallengeFinished", DtmRacingUI.NEXT_MAIN);
		// abspielen verfolgen
		onEnterFrame = function() {
			follower.followTimeline();
		}
		// abspielen
		gotoAndPlay("frClose");
	}
	
	public function startRace():Void
	{
		// abspielen verfolgen
		var follower:TimelineFollower = new TimelineFollower(this, "onChallengeFinished", DtmRacingUI.NEXT_RACE);
		// abspielen verfolgen
		onEnterFrame = function() {
			follower.followTimeline();
		}
		// abspielen
		gotoAndPlay("frClose");
	}
	
	public function onChallengeFinished(next:Number ):Void
	{
		// zur main
		if (next == DtmRacingUI.NEXT_MAIN) {
			myDtmRacingUI.showMain();
		// zum race
		} else if (next == DtmRacingUI.NEXT_RACE) {
			myDtmRacingUI.showRace();
		}
	}
	
	public function getInfobox():InfoboxChallengeUI
	{
		// zurueck geben
		return challenge_info_mc;
	}
	
	private function initHeadline(status:Number ):Void
	{
		// je nach status
		switch (status) {
			// herausforderung
			case Challenge.STATUS_NEW :
			
				headline_txt.text = "Herausforderung";
				// streckauswahl anschalten
				chooser_track_mc.setMode(ChooserUI.MODE_SELECT);
			
				break;
			// herausforderung annehmen
			case Challenge.STATUS_CHALLENGER_DONE :
			
				headline_txt.text = "Herausforderung annehmen";
				// streckauswahl abschalten
				chooser_track_mc.setMode(ChooserUI.MODE_SHOW);
			
				break;
			// trainieren
			case Challenge.STATUS_TRAINING :
			
				headline_txt.text = "Trainieren";
				// streckauswahl anschalten
				chooser_track_mc.setMode(ChooserUI.MODE_SELECT);
			
				break;
			// gast
			case Challenge.STATUS_GUEST :
			
				headline_txt.text = "Gast";
				// streckauswahl anschalten
				chooser_track_mc.setMode(ChooserUI.MODE_SELECT);
				// button zurueck ausblenden
				back_btn._visible = false;
			
				break;
			
		}
		// in versalien
		headline_txt.text = headline_txt.text.toUpperCase();
	}
}