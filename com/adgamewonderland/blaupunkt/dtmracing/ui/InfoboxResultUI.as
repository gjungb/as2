/* 
 * Generated by ASDT 
*/ 

/*
klasse:			InfoboxResultUI
autor: 			gerd jungbluth, adgame-wonderland
email:			gerd.jungbluth@adgame-wonderland.de
kunde:			blaupunkt
erstellung: 		15.06.2005
zuletzt bearbeitet:	20.06.2005
durch			gj
status:			in bearbeitung
*/

import com.adgamewonderland.blaupunkt.dtmracing.challenge.*;

import com.adgamewonderland.blaupunkt.dtmracing.ui.*;

class com.adgamewonderland.blaupunkt.dtmracing.ui.InfoboxResultUI extends InfoboxUI {
	
	private var challengermessage_mc:MovieClip;
	
	private var guestinfo_mc:MovieClip;
	
	private var boxes3_mc:MovieClip;
	
	private var send_btn:Button;
	
	private var back_btn:Button;
	
	private var again_btn:Button;
	
	public function InfoboxResultUI()
	{
		// vererbung
		super.constructor.apply(super, arguments);
		// alle ausblenden
		challengermessage_mc._visible = guestinfo_mc._visible = boxes3_mc._visible = false;
		send_btn._visible = back_btn._visible = again_btn._visible = false;
	}
	
	public function initInfobox(status:Number ):Void
	{
		// je nach status
		switch (status) {
			// herausforderung
			case Challenge.STATUS_NEW :
				// box einblenden
				boxes3_mc._visible = true;
				// und abspielen
				boxes3_mc.play();
				// button herausforderung senden
			 	send_btn.onRelease = function () {
			 		this._parent.sendChallenge();
			 	};
			
				break;
			// trainieren
			case Challenge.STATUS_TRAINING :
				// button zurueck
				back_btn._visible = true;
				// button rennen neu starten
				again_btn._visible = true;
				// button zurueck
			 	back_btn.onRelease = function () {
			 		this._parent._parent.stopResult(Challenge.STATUS_TRAINING);
			 	};
				// button rennen neu starten
			 	again_btn.onRelease = function () {
			 		this._parent._parent.startRace();
			 	};
			
				break;
			// gast
			case Challenge.STATUS_GUEST :
				// box einblenden
				boxes3_mc._visible = true;
				// und abspielen
				boxes3_mc.play();
				// button zurueck
			 	send_btn.onRelease = function () {
			 		this._parent._parent.stopResult(Challenge.STATUS_GUEST);
			 	};
			
				break;
			
		}
		// abspielen verfolgen
		onEnterFrame = function() {
			// nacheinander einblenden
			if (_currentframe == _totalframes - 1 && status == Challenge.STATUS_NEW) challengermessage_mc._visible = send_btn._visible = true;
			if (_currentframe == _totalframes - 1 && status == Challenge.STATUS_GUEST) guestinfo_mc._visible = send_btn._visible = true;
			if (_currentframe == _totalframes - 0) delete(onEnterFrame);
		};
		// abspielen
		play();
	}
	
	public function sendChallenge():Void
	{
		// nachricht an den gegner
		var message:String = challengermessage_mc.message_txt.text;
		// herausforderung senden
		_parent.sendChallenge(message);
	}
}