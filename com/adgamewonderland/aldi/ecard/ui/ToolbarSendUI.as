/* 
 * Generated by ASDT 
*/ 

import com.adgamewonderland.agw.*;

import com.adgamewonderland.aldi.ecard.beans.*;

import com.adgamewonderland.aldi.ecard.ui.*;

class com.adgamewonderland.aldi.ecard.ui.ToolbarSendUI extends MovieClip {
	
	private var myToolbarUI:ToolbarUI;
	
	private var myLso:SharedObject;
	
	private var sendername_txt:TextField;
	
	private var senderemail_txt:TextField;
	
	private var receivername_txt:TextField;
	
	private var receiveremail_txt:TextField;
	
	private var message_txt:TextField;
	
	private var error_txt:TextField;
	
	private var send_btn:Button;
	
	private var compose_btn:Button;
	
	public function ToolbarSendUI() {
		myToolbarUI = ToolbarUI(_parent);
		// lso zum speichern / laden der eingaben
		myLso = SharedObject.getLocal("aldiecard");
		// absenden
		send_btn.onRelease = function() {
			this._parent.onSend();
		}
		// ecard bearbeiten
		compose_btn.onRelease = function() {
			this._parent.onCompose();
		}
		// gespeicherte eingaben anzeigen
		showStoredData();
		// tabsetter
		var index:Number = 0;
		sendername_txt.tabIndex = ++index;
		senderemail_txt.tabIndex = ++index;
		receivername_txt.tabIndex = ++index;
		receiveremail_txt.tabIndex = ++index;
		message_txt.tabIndex = ++index;
	}
	
	public function onSend():Void
	{
		// meldung
		error_txt.text = "";
		// buttons deaktivieren
		send_btn.enabled = false;
		compose_btn.enabled = false;
		
		// sender
		var sender:User = new User();
		sender.setName(sendername_txt.text);
		sender.setEmail(senderemail_txt.text);
		// receiver
		var receiver:User = new User();
		receiver.setName(receivername_txt.text);
		receiver.setEmail(receiveremail_txt.text);
		// message
		var message:String = escape(message_txt.text);
		
		// abbrechen, wenn fehlerhaft
		if (!checkForm(sender, receiver)) {
			// meldung
			error_txt.text = "Angaben nicht korrekt!";
			// buttons aktivieren
			send_btn.enabled = true;
			compose_btn.enabled = true;
			// abbrechen
			return;
		}
		
		// gespeicherte eingaben loeschen
		clearStoredData();
		// ecard senden
		_global.EcardUI.sendEcard(sender, receiver, message);
	}
	
	public function onCompose():Void
	{
		// eingaben speichern
		storeData();
		// ecard bearbeiten
		myToolbarUI.setMode(ToolbarUI.MODE_COMPOSE);
		// stage editierbar
		_global.EcardUI.getStage().setEditable(true);
	}
	
	private function storeData():Void
	{
		// sender
		var sender:User = new User();
		sender.setName(sendername_txt.text);
		sender.setEmail(senderemail_txt.text);
		// sender lokal speichern
		myLso.data.sender = sender;
		// receiver
		var receiver:User = new User();
		receiver.setName(receivername_txt.text);
		receiver.setEmail(receiveremail_txt.text);
		// receiver lokal speichern
		myLso.data.receiver = receiver;
		// message
		var message:String = message_txt.text;
		// message lokal speichern
		myLso.data.message = message;
		
		// speichern
		myLso.flush();
	}
	
	private function showStoredData():Void
	{
		// sender
		var sender:User = myLso.data.sender;
		if (sender != null) {
			sendername_txt.text = sender["name"];
			senderemail_txt.text = sender["email"];
		}
		// receiver
		var receiver:User = myLso.data.receiver;
		if (receiver != null) {
			receivername_txt.text = receiver["name"];
			receiveremail_txt.text = receiver["email"];
		}
		// message
		var message:String = myLso.data.message;
		if (message != undefined) message_txt.text = message;
	}
	
	private function clearStoredData():Void
	{
		// sender lokal speichern
		myLso.data.sender = null;
		// receiver lokal speichern
		myLso.data.receiver = null;
		// message lokal speichern
		myLso.data.message = "";
		
		// speichern
		myLso.flush();
	}
	
	private function checkForm(sender:User, receiver:User ):Boolean
	{
		// eingaben valide
		var valid:Boolean = false;
		// formprocessor
		var fp:Formprocessor = new Formprocessor();
		// fehler
		var errors:Array = fp.checkForm([1, "sendername", sender.getName(), 3, "senderemail", sender.getEmail(), 1, "receivername", receiver.getName(), 3, "receiveremail", receiver.getEmail()]);
		// valide, wenn keine fehler
		valid = (errors.length == 0);
		// zureuck geben
		return valid;
	}
}