/* 
 * Generated by ASDT 
*/ 

import mx.utils.Delegate;

import com.adgamewonderland.aldi.pipeman.beans.Cell;
import com.adgamewonderland.aldi.pipeman.beans.Pipe;
import com.adgamewonderland.aldi.pipeman.ui.PipeUI;

import de.kruesch.event.EventBroadcaster;

class com.adgamewonderland.aldi.pipeman.ui.CellUI extends MovieClip {
	
	public static var WIDTH:Number = 25;
	
	public static var HEIGHT:Number = 25;
	
	public static var ALPHA:Number = 75; // 25
	
	private var _cell:Cell;
	
	private var cell:Cell;
	
	private var pipe_mc:PipeUI;
	
	private var bomb_mc:PipeUI;
	
	private var _event:EventBroadcaster;
	
	public function CellUI() {
		this.cell = _cell;
		this.onRollOver = Delegate.create(this, onCellOver);
		this.onRollOut = this.onDragOut = this.onReleaseOutside = Delegate.create(this, onCellOut);
		this.onPress = Delegate.create(this, onCellPress);
		this.useHandCursor = false;
		_event = new EventBroadcaster();
	}
	
	public function addListener(l ):Void
	{
		_event.addListener(l);
	}
	
	public function removeListener(l ):Void
	{
		_event.removeListener(l);
	}
	
	public function onCellOver():Void
	{
		// event
		_event.send("onCellUIOver", this);
	}
	
	public function onCellOut():Void
	{
		// event
		_event.send("onCellUIOut", this);
	}
	
	public function onCellPress():Void
	{
		// event
		_event.send("onCellUIPress", this);
	}
	
	public function addPipe(pipe:Pipe, transparent:Boolean ):Void
	{
		// alpha
		var alpha:Number = (transparent ? ALPHA : 100);
		// pipe oder bomb
		if (pipe.getType() != Pipe.TYPE_BOMB) {
			// neue pipe auf buehne
			pipe_mc = PipeUI(this.attachMovie("PipeUI", "pipe_mc", 1, {_alpha : alpha, _pipe : pipe}));
			
		} else {
			// neue bomb auf buehne
			bomb_mc = PipeUI(this.attachMovie("PipeUI", "bomb_mc", 2, {_alpha : alpha, _pipe : pipe}));
		}
	}
	
	public function removePipe():Void
	{
		// pipe auf buehne loeschen
		if (pipe_mc instanceof PipeUI) pipe_mc.removeMovieClip();
	}
	
	public function removeBomb():Void
	{
		// bomb auf buehne loeschen
		if (bomb_mc instanceof PipeUI) bomb_mc.removeMovieClip();
	}
	
	public function getPipeUI():PipeUI
	{
		return pipe_mc;	
	}
	
	public function getCell():Cell {
		return cell;
	}

	public function setCell(cell:Cell):Void {
		this.cell = cell;
	}

}